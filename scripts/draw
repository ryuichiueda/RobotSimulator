#!/bin/bash

tmp=/tmp/$$

cat $1 |
gawk 'BEGIN{
	w = 400
	for(y=-w;y<=w;y++){
		for(x=-w;x<=w;x++){
			r[x,y] = 255
			g[x,y] = 255
			b[x,y] = 255
		}
	}
}
$1=="PARTICLE"{
	x = int(($2 + 0.5)/5);
	y = int(($3 + 0.5)/5);
	theta = $4 / 180.0 * 3.141592;
	weight = $5;

	r[x,y] = 0; g[x,y] = 0; b[x,y] = 0;
	r[x+1,y] = 0; g[x+1,y] = 0; b[x+1,y] = 0;
	r[x-1,y] = 0; g[x-1,y] = 0; b[x-1,y] = 0;
	r[x,y+1] = 0; g[x,y+1] = 0; b[x,y+1] = 0;
	r[x,y-1] = 0; g[x,y-1] = 0; b[x,y-1] = 0;

	for(i=0;i<16;i++){
		dx = int(x + i * 0.5 * cos(theta) + 0.5);
		dy = int(y + i * 0.5 * sin(theta) + 0.5);
		r[dx,dy] = 0;
	}
}
$1=="GOAL"{
	x = $2/5;
	y = $3/5;
        theta = 45.0 / 180 * 3.141592
	for(i=-10;i<=10;i++){
		dx = int(0.5 + x + i*cos(theta));
		dy = int(0.5 + y + i*sin(theta));
		r[dx,dy] = 0; g[dx,dy] = 0; b[dx,dy] = 0;
		dx = int(0.5 + x - i*cos(theta));
		dy = int(0.5 + y + i*sin(theta));
		r[dx,dy] = 0; g[dx,dy] = 0; b[dx,dy] = 0;
	}
}
$1=="LANDMARK"{
	x = $2/5;
	y = $3/5;
	size = 5;
	for(i=0;i<360;i++){
		j = i / 180.0 * 3.141592;
		dx = int(0.5 + x + size*cos(j));
		dy = int(0.5 + y + size*sin(j));
		r[dx,dy] = 0; g[dx,dy] = 0; b[dx,dy] = 255;
	}
}
$1=="ROBOT"{
	x = $2/5;
	y = $3/5;
	theta = $4 / 180.0 * 3.141592;
	size = $5/5;

	for(i=0;i<360;i++){
		j = i / 180.0 * 3.141592;
		dx = int(0.5 + x + size*cos(j));
		dy = int(0.5 + y + size*sin(j));
		r[dx,dy] = 255; g[dx,dy] = 0; b[dx,dy] = 0;
	}
	for(i=0;i<=size;i++){
		dx = int(0.5 + x + i*cos(theta));
		dy = int(0.5 + y + i*sin(theta));
		r[dx,dy] = 255; g[dx,dy] = 0; b[dx,dy] = 0;
	}
}
END{
	print "P3",w*2+1,w*2+1,256;
	for(y=w;y>=-w;y--){
		for(x=-w;x<=w;x++){
			print r[x,y], g[x,y], b[x,y];
		}
	}
}'	> $tmp-ppm.ppm

convert $tmp-ppm.ppm $tmp-png.png

cat $tmp-png.png

rm -f $tmp-*
exit 0
